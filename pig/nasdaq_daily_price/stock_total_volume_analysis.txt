
-- load the data storage directory
data = load '/user/cloudera/rawdata/hadoop_class/sqoop/nasdaq_daily_prices' using PigStorage('\t') as (exchange:chararray,stock_symbol:chararray,date:chararray,stock_price_open:float,stock_price_high:float,stock_price_low:float,stock_price_close:float,stock_volume:int,stock_price_adj_close:float);

-- incase the data from sqoop still contains the header, it is important to filter it out
filtered_data = filter data by exchange != 'exchange';

-- project only the stock_symbol and the volumn
proj_data = foreach filtered_data generate stock_symbol, stock_volume;

--group the available records by the symbol
grp_data = group proj_data by stock_symbol;

--reduce the grouped data by summing the groups
agg_data = foreach grp_data generate group as symbol, SUM(proj_data.stock_volume);

-- store records in the ouput location
store agg_data into '/user/cloudera/output/hadoop_class/nasdaq_stock_volume_pig';